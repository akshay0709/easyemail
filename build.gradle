plugins {
    id 'java-library'
    id 'maven'
    id 'signing'
}

group = 'com.github.akshay0709'
archivesBaseName = 'easyemail'
version = '1.0.0'

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

if(project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')){
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'EasyEmail'
                    packaging 'jar'
                    description 'EasyEmail is an open source and easy to use library to send event driven emails via Java code.'
                    url 'https://github.com/akshay0709/easyemail'

                    scm {
                        connection 'https://github.com/akshay0709/easyemail.git'
                        developerConnection 'git@github.com:akshay0709/easyemail.git'
                        url 'https://github.com/akshay0709/easyemail'
                    }

                    licenses {
                        license {
                            name 'MIT License'
                            url 'https://github.com/akshay0709/easyemail/blob/master/LICENSE.txt'
                        }
                    }

                    developers {
                        developer {
                            id 'akshay0709'
                            name 'Akshay Pawar'
                            email 'akshay070993@gmail.com'
                        }
                    }
                }
            }
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    testCompile (
            [group: 'junit', name: 'junit', version: '4.12'],
            [group: 'com.icegreen', name: 'greenmail', version: '1.5.8']
    )
    compile group: 'javax.mail', name: 'mail', version: '1.4.7'
}